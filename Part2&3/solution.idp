/** 
* Skeleton for the second part of the project. 
*
* No names in this skeleton can be changed, you can add things to the vocabulary and you should definitely add stuff to the theory. 
*/
include<LTC>

LTCvocabulary warehousevoc{
	type Time isa nat
	Start: Time
	partial Next(Time):Time

	type pallet
	type action constructed from {rotateRight, rotateLeft, moveFront, moveBack, pickUp(pallet), putDown(pallet)}	
	execute(Time,action)

	on(Time,pallet,pallet)
	carried(Time,pallet)

	type xco isa nat
	type yco isa nat	
	type location constructed from {grid(xco,yco)}
	inaccessible(location)
	robotposition(Time): location
	position(Time,pallet): location
	
	type dir constructed from {north, east, south, west}
	facing(Time): dir
    
	
	type height isa nat
	ceiling: height
    
    //function which maps a pallet to its height
    heightOf(Time, pallet): height
    
    C_Facing(Time, dir)
    C_NFacing(Time, dir)
    I_Facing(dir)
    
    C_Robotposition(Time, location)
    C_NRobotposition(Time, location)
    I_Robotposition(location)
    
    
    C_Carried(Time, pallet)
    C_NCarried(Time,pallet)
    I_Carried(pallet)
    
    C_On(Time, pallet, pallet)
    C_NOn(Time, pallet, pallet)
    I_On(pallet, pallet)
    
    C_Position(Time, pallet, location)
    C_NPosition(Time, pallet, location)
    I_Position(pallet, location)
    
    locationInFront(Time, location)
    locationBehind(Time, location)
}

theory solution: warehousevoc{
    
    /**** TIME THEORY****/
    {
        Start = MIN[:Time].
        !t : Next(t) = t + 1 <- Time(t +1).
    }
    
    
    ! t [Time] : ?=<1 a[action]: execute(t, a).
    
    

/************************ The static predicates from the 1st part *************************/
            
/*** the pallets ***/
    // at each moment, there is at most one pallet carried by the robot
    ! t [Time]: ?=<1 p1 [pallet] : carried(t,p1).
    
    // at each moment, if a pallet is being carried, then there is no pallet above or below that pallet
    ! t [Time] p1 [pallet]: carried(t,p1) => ~? p2 [pallet] : on(t, p2, p1)|on(t, p1,p2).
    
    //  at each moment, there  can be at most one pallet on top of a pallet
   // ! t [Time] p1 [pallet]: ?=<1 p2 [pallet]: on(t, p1, p2).
    ! t [Time] p1 [pallet]: ?=<1 p2 [pallet]: on(t, p2, p1).
    
    //  at each moment, there can be at most one pallet below a pallet
    //! t [Time] p1 [pallet]: ?=<1 p2 [pallet]: on(t, p2, p1).
    ! t [Time] p1 [pallet]: ?=<1 p2 [pallet]: on(t, p1, p2).
    
    // at each moment, for all pallets, there cannot be a pallet which is at the same time above and below that pallet
    ! t [Time] p1 , p2 [pallet]: ~(on(t,p1,p2)&on(t,p2,p1)).
    
    // at each moment, a pallet cannot be on top of itself
    ! t [Time] p1 [pallet]: ~on(t,p1,p1).
    
    
    
    //  at each moment, if a location is inaccessible, then there cannot be pallet positioned at that location
    ! t [Time] l1 [location]: inaccessible(l1) => ~? p1 [pallet]: position(t,p1) = l1.
    
    /* at each moment, the height of a pallet is at least 1 (on the ground or being carried)
     and the heigth of a pallet is at most the height of the ceiling */
    ! t [Time] p1 [pallet] : 1 =< heightOf(t,p1) & heightOf(t,p1) =< ceiling.
    
    // at each moment, a pallet is being carried if and only if its position is the robot's postion
    ! t [Time] p1 [pallet]: carried(t,p1) <=> position(t,p1) = robotposition(t).
       
    
    //If a pallet is on top of another, they have to have the same position
    ! t [Time] p1, p2 [pallet]: on(t, p2, p1) => (position(t, p1) = position(t, p2)).
    
/*** the robot ***/
    // at each moment, the robot always faces exactly one direction
     ! t [Time]: ?1 d [dir]: facing(t) = d.
    
    // at each moment, if a location is inaccessible, then the robot cannot stand on that location
    ! t [Time] l1 [location]: inaccessible(l1) => robotposition(t) ~= l1.
    
    // at each moment, there cannot be a pallet on the robot's position if it isn't carried
	! t [Time] l1 [location] : (robotposition(t) = l1)=> ~(? p1 [pallet]: position(t,p1) = l1 & ~carried(t,p1)).    
    
    
/*** height ***/
    //at each moment, if two different pallets have the same position, then they cannot have the same height
    ! t [Time] p1 [pallet] p2 [pallet]: heightOf(t,p1)~=heightOf(t,p2) <= position(t,p1) = position(t,p2) &p1 ~= p2.
   
    // height is inductively defined
    {
     //if a pallet is being carried, then it must have a height of 1
     ! t [Time] p1 [pallet]: heightOf(t,p1) = 1  <- carried(t,p1).
     
     /* for all pallets p1: if there isn't a pallet below p1 and if p1 isn't being carried, then the height of p1 is 1. This is a base case which defines the height for pallets lying on the ground.       
        */
     ! t [Time] p1 [pallet]: heightOf(t,p1)= 1<- (~? p2 [pallet]: on(t,p1, p2)&~carried(t,p1)).  
        
     // if pallet 2 is on top of pallet 1, then pallet 2 has a height which is 1 higher
     ! t [Time] p1, p2 [pallet]: heightOf(t,p1) = heightOf(t,p2)+1 <- on(t,p1,p2). 
  
    }
    
/************************** The second part *************************/

    
/* Inductive definition of locationInFront and locationBehind */

    
        {
        ! t[Time] x[xco] y[yco]:
        locationInFront(t,grid(x,y+1)) <-
        (facing(t) = north) & (robotposition(t) = grid(x,y)).

        ! t[Time] x[xco] y[yco]:
        locationInFront(t, grid(x+1,y)) <-
        (facing(t) = east) & (robotposition(t) = grid(x,y)).

        ! t[Time] x[xco] y[yco]:
        locationInFront(t, grid(x,y-1)) <-
        (facing(t) = south) & (robotposition(t) = grid(x,y)).

        ! t[Time] x[xco] y[yco]:
        locationInFront(t, grid(x-1,y)) <-
        (facing(t) = west) & (robotposition(t) = grid(x,y)).
    }
    
        {
        ! t[Time] x[xco] y[yco]:
        locationBehind(t, grid(x,y-1)) <-
        (facing(t) = north) & (robotposition(t) = grid(x,y)).

        ! t[Time] x[xco] y[yco]:
        locationBehind(t,grid(x-1,y)) <-
        (facing(t) = east) & (robotposition(t) = grid(x,y)).

        ! t[Time] x[xco] y[yco]:
        locationBehind(t,grid(x,y+1)) <-
        (facing(t) = south) & (robotposition(t) = grid(x,y)).

        ! t[Time] x[xco] y[yco]:
        locationBehind(t,grid(x+1,y)) <-
        (facing(t) = west) & (robotposition(t) = grid(x,y)).
    }
    
    
    
/*** FACING ***/
    //Inertia
    {
        // for all directions d: if d is the initial direction, then the robot faces d at time o
       ! d [dir]: facing(0)=d <-I_Facing(d).
        // if there is a cause for the robot to change its direction to d at time t, the robot will be facing d at time Next(t)
       ! d [dir] t [Time]: facing(Next(t))= d <- C_Facing(t,d).
       ! d [dir] t [Time]: facing(Next(t))=d <- (facing(t)=d) &~C_NFacing(t,d).
    }
    //Causes
    {
      ! t [Time] d, d2 [dir]: C_NFacing(t,d) <- C_Facing(t,d2) & d~=d2.
     
      //Cases for each direction the robot is facing and which direction it should be facing next based on the way the robot is rotating. 
      ! t [Time]: C_Facing(t, north) <- ((facing(t) = west) & execute(t, rotateRight) ) | ((facing(t) = east) & execute(t, rotateLeft) ).
      ! t [Time]: C_Facing(t, east) <- ((facing(t) = north) & execute(t, rotateRight) ) | ((facing(t) = south) & execute(t, rotateLeft) ).
      ! t [Time]: C_Facing(t, south) <- ((facing(t) = east)& execute(t, rotateRight) ) | ((facing(t) = west) & execute(t, rotateLeft) ).    
      ! t [Time]: C_Facing(t, west) <- ((facing(t) = south) & execute(t, rotateRight) ) | ((facing(t) = north)& execute(t, rotateLeft) ).  

      ! d[dir]: I_Facing(d) <- facing(0) = d.
    }
    
    
/*** MOVING ***/
    //Inertia
    {
       //For all locations l: if l is the initial location, the robot position is there.
       ! l [location]: robotposition(0)=l <- I_Robotposition(l).
       //If there is a cause for the robot to change its position to l, the robotposition will be l at the next time step.
       ! t [Time] l [location]: robotposition(Next(t))=l <- C_Robotposition(t,l).
       //If the robot does not have a cause to change position, it should keep its current position.
       ! t [Time] l [location]: robotposition(Next(t))=l <- (robotposition(t)=l) & ~C_NRobotposition(t,l).        
    }
    //Causes
    {	
        //If a robot changes position to l2, it causes the robot to not be at l1 where l1 =/= l2
        ! t [Time] l1, l2 [location]: C_NRobotposition(t,l1) <- (robotposition(t)= l1)& C_Robotposition(t,l2) & (l1 ~=l2).
        //if a robot moves forward, it causes its position to be the locationInFront at that time.
        ! t [Time] l [location] : C_Robotposition(t, l) <- execute(t,moveFront)& locationInFront(t,l).
        //if a robot moves backward, it causes its position to be the locationInack at that time.
        ! t [Time] l [location]: C_Robotposition(t, l) <- execute(t,moveBack) & locationBehind(t,l).
        //By combining this and the Inertia part, all initial positions are checked.
        ! l [location] : I_Robotposition(l) <- robotposition(0) = l.
    }
    
/*** CARRYING ***/
    
    //Preconditions
    //If the robot picks up a pallet, the pallet needs to be in front of the robot and there cannot be a pallet on top of it.
    ! t [Time] p [pallet]: execute(t, pickUp(p)) => locationInFront(t,position(t, p)) & ~?p2 [pallet]: on(t, p2, p).
    //To put down a pallet, the robot must be carrying that pallet
    ! t [Time] p [pallet]: execute(t, putDown(p)) => carried(t, p).
    
    //Inertia
    {	
        //Initial
        ! p [pallet]: carried(0,p) <- I_Carried(p).
        //if there is a cause to carry a pallet, it will be carried a the next time step.
        ! t [Time] p [pallet]: carried(Next(t), p) <- C_Carried(t, p).
        //If there is not a cause to not carry a pallet, the pallet will keep on being carried.
        ! t [Time] p [pallet]: carried(Next(t), p) <- carried(t,p) & ~C_NCarried(t,p).
    }
    //Causes
    {	
        //If the robot executes a pickUp of a pallet, it is a cause for a pallet to be carried.
        ! t [Time] p [pallet]: C_Carried(t,p) <- execute(t, pickUp(p)).
        //if the robot executes a putDown of a pallet, it is a cause for that pallet to not be carried anymore.
        ! t [Time] p [pallet]: C_NCarried(t,p) <- execute(t, putDown(p)).
        ! p [pallet]: I_Carried(p) <- carried(0,p).        
    }
    
    
/*** ON ***/
    //Inertia
    {	
        //Initial
        ! p1, p2 [pallet]: on(0, p1, p2) <- I_On(p1,p2).
        //if there is a cause for a pallet to be on another pallet, it will be in the next time step.
     	! t [Time] p1, p2 [pallet]: on(Next(t), p1, p2) <- C_On(t,p1,p2).
        //If there is not a cause to not be on another pallet anymore, all pallets will stay where they are.
     	! t [Time] p1, p2 [pallet]: on(Next(t), p1,p2) <- on(t,p1,p2) & ~C_NOn(t,p1,p2).
    }
    //Causes
    
    {
        //If the robot executes a putDown of p2, p1 is in front of the robot and there is not a p3 on top of p1, there is a cause for p2 to be on p1
     	! t [Time] p1, p2 [pallet]: C_On(t, p2, p1) <- execute(t, putDown(p2)) & locationInFront(t,position(t, p1)) & ~?p3 [pallet] : on(t, p3, p1).
        //If the robot executes a pickUp of p2, it is a cause for p2 to not be on any p1.
        ! t [Time], p1, p2 [pallet]: C_NOn(t, p2, p1) <- execute(t, pickUp(p2)).
        ! p1,p2 [pallet]: I_On(p1,p2) <- on(0, p1, p2).
    }
/*** POSITION ***/
    //Inertia
    {	
        //initial
        ! p [pallet] l [location]:position(0, p) = l <- I_Position(p, l).
        //If there is a cause for a pallet to be on a location, it will be in the next timestep.
        ! t [Time] p [pallet] l [location]: position(Next(t), p) = l <- C_Position(t, p, l).
        //If there is not a cause for a pallet to not be on a location, it will stay where it is.
        ! t [Time] p [pallet] l [location]: position(Next(t), p) = l <- position(t, p) = l & ~C_NPosition(t, p, l).
    }
    //Causes
    {	
        //If a robot executes pickUp of p, it is a cause for p to change location to robotposition at that time.
        ! t [Time] p [pallet]: C_Position(t, p, robotposition(t)) <- execute(t, pickUp(p)).
        //If a robot executes putDown of p, the location of p should change to the position in front of the robot.
        ! t [Time] p [pallet] l [location]: C_Position(t, p, l) <- execute(t, putDown(p)) & locationInFront(t, l).
        //if there is a cause for the robot position to change and p is carried at that time, the position of p should also change.
        ! t [Time] p [pallet] l [location]: C_Position(t, p, l) <- C_Robotposition(t, l) & carried(t, p).
        
        //If the robot executes a pickUp of p, it changes the location of p to be on another position.
        ! t [Time] p [pallet]: C_NPosition(t, p, position(t,p)) <- execute(t, pickUp(p)).
        //If the robot puts down a pallet, it causes the pallet to not be on its last location anymore
        ! t [Time] p [pallet]: C_NPosition(t, p, position(t,p)) <- execute(t, putDown(p)).
        //If there is a cause for the robot to not be on the same position anymore and p is being carried, that is a cause for the position of p to change too.
        ! t [Time] p [pallet] l [location]: C_NPosition(t, p, l) <- C_NRobotposition(t, l) & carried(t, p).
        
        ! p [pallet] l [location]: I_Position(p, l) <- position(0, p) = l.
    }
    

    
}


/* Negative theory for verifying whether it is possible to make a stack containing all pallets, Negative because there are too man models otherwise */
theory verification1: warehousevoc{
	
    ? t [Time] l [location]: ! p [pallet]: position(t,p)=l.
}

procedure verification1proc(struc){
	local theory1 = merge(solution, verification1)
	local sols = modelexpand(theory1, struc)
	if #sols > 0 then
		print(">>>verification 1 succeeded. One of the models found is:")
		print(sols[1])
		return
	end
	print(">>>verification 1 failed!")
}

//Verify if there can be a stack containing one pallet for at least 3 time steps.
theory verification2: warehousevoc{
	~(! t [Time] l [location] p1 [pallet] p2 [pallet]: (on(t,p2,p1) & position(t,p1) = l & Time(t+3)) => //t+3 to make sure you actually have next moments
    	(position(Next(t),p1)=l & position(Next(Next(t)),p1)=l & position(Next(Next(Next(t))),p1)=l)).
}
procedure verification2proc(struc){
	local theory2 = merge(solution, verification2)
	local sols = modelexpand(theory2, struc)
	if #sols > 0 then
		print(">>>verification 2 failed. This model is faulty:")
		print(sols[1])
		return
	end
	print(">>>verification 2 success!")
}


/* Theory for verifying whether the robot can drive through pallets (should fail)*/
theory verification3: warehousevoc{
	?t [Time] p [pallet]: ~carried(t,p) & position(t,p) = robotposition(t).
}

procedure verification3proc(struc){
	local theory3 = merge(solution, verification3)
	local sols = modelexpand(theory3, struc)
	if #sols > 0 then
		print(">>>verification 3 failed. This is a faulty model:")
		print(sols[1])
		return
	end
	print(">>>verification 3 success!")
}


procedure allVerifications(){
	strucToCheck = verStruc
	verification1proc(strucToCheck)
	verification2proc(strucToCheck)
	verification3proc(strucToCheck)
}


structure verStruc:warehousevoc{
	Time = {0..16}
	Start = 0
	Next = {0->1;1->2;2->3;3->4;4->5;5->6;6->7;7->8;8->9;9->10;10->11;11->12;12->13;13->14;14->15;15->16}
	xco={1..3}
	yco={1..3}
	height = {0..3}
	pallet={p1;p2}
	inaccessible={grid(3,2);grid(2,2)}
	ceiling = 3
}


procedure main(){
    //Depending on what you wish to do, execute one of the following methods:
    print("Main")
	 
    //This method (defined in the instances.idp) checks some given worlds for consistency
	//check()
    
    allVerifications()

    //This method lets you walk through your solutions interactively
	//simulate_interactive(solution,verStruc)
	
    //This method walks randomly trough the solutions, 5 steps deep
	//simulate_random(solution,verStruc,5)
	
    //This method (defined above) simply searches models of your theory
	//stdoptions.nbmodels=5 //Find 5 models	
	//printmodels(modelexpand(solution,verStruc))
	
}
